apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'

apply from: 'https://raw.githubusercontent.com/littledot/android-gradle-java-template/v1.0.0/common.gradle'

initFabricProperties()

android {
    compileSdkVersion project.compileSdk
    buildToolsVersion project.buildTools

    defaultConfig {
        applicationId "com.permissionnanny"
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk
        versionCode project.versionCode
        versionName project.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(file('fabric.properties').newDataInputStream())
        manifestPlaceholders = [fabricApiKey: properties.getProperty("apiKey")]
    }
    signingConfigs {
        release {
            storeFile project.rootProject.file('google-play.jks')
            storePassword project.GOOGLE_PLAY_STORE_PASSWORD
            keyAlias project.GOOGLE_PLAY_KEY_ALIAS
            keyPassword project.GOOGLE_PLAY_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        lintConfig project.rootProject.file('lint.xml')
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/commonTest/java'
            java.srcDirs += 'src/test/kotlin'
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    kapt project.daggerCompiler

    compile project.design
    compile project.supportV4
    compile project.appcompatV7
    compile project.cardviewV7
    compile project.recyclerviewV7

    compile project(':common')
    compile project(':lib')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') { transitive = true; }
    compile project.ankoSdk15
    compile project.dagger
    compile project.butterknife
    compile project.javaxAnnotation
    compile project.javaxInject
    compile project.kotlinStdlib
    compile project.kryo
    compile project.leveldb
    compile project.mbassador
    compile project.timber
    debugCompile project.leakcanaryAndroid
    releaseCompile project.leakcanaryAndroidNoOp

    kaptTest project.daggerCompiler

    testCompile project(':commonTest')
    testCompile project.kotlinTest
    testCompile project.kotlinTestJunit

    androidTestCompile project.junit
    androidTestCompile project.mockitoCore
    androidTestCompile project.dexmakerMockito
    androidTestCompile project.runner
    androidTestCompile project.rules
    androidTestCompile project.espressoCore
    androidTestCompile project.asm
    androidTestCompile project.objenesis
}

def initFabricProperties() {
    if (System.env.CI) {
        def commentMessage = "Contains API Secret used to validate your application. Commit to internal source control; avoid making secret public."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: "${System.env.CRASHLYTICS_API_SECRET}")
            entry(key: "apiKey", value: "${System.env.CRASHLYTICS_API_KEY}")
        }
    }
}

// Try reducing initial heap size. https://discuss.gradle.org/t/gradle-travis-ci/11928
if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
